<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml"
				width="400"
				height="154"
				title="上传文件"
				close="close()"
				layout="absolute"
				showCloseButton="true"
				backgroundImage="@Embed(source='assets/images/popwindowbg.png')"
				backgroundSize="100%"
				creationComplete="init();">
	<mx:Script>
		<![CDATA[
			import common.actionscript.MyPopupManager;
			
			import flash.events.*;
			import flash.net.FileReference;
			
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			import mx.managers.PopUpManager;
			
			public var file: FileReference;
			private var _filelocation:String;
			
			

			public function get filelocation():String
			{
				return _filelocation;
			}

			public function set filelocation(value:String):void
			{
				_filelocation = value;
			}

			private function init(): void{
				this.filelocation="";
				Security.allowDomain("*");
				file = new FileReference();
				file.addEventListener(ProgressEvent.PROGRESS, onProgress);
				file.addEventListener(Event.SELECT, onSelect);
				file.addEventListener(Event.COMPLETE, completeHandle);
			}
			
			private function completeHandle(event:Event):void{
				Alert.show("恭喜你，上传成功!");
				this.dispatchEvent(new Event("RefreshCurrentFolder"));
				PopUpManager.removePopUp(this);
			}
			
			private function upload(): void{
				
				var imageTypes:FileFilter = new FileFilter("Images (*.jpg, *.jpeg, *.png)", "*.jpg;*.jpeg;*.png");				
				var textTypes:FileFilter = new FileFilter("Documents (*.txt, *.doc,*.xls,*.pdf,*.rtf)", "*.txt; *.doc;*.xls;*.pdf;*.rtf");
				var codeTypes:FileFilter = new FileFilter("code(*.mxml,*.html,*.java)", "*.mxml;*.html;*.java");
				var arcTypes:FileFilter = new FileFilter("Archives(*.zip,*.rar)", "*.zip;*.rar");
                var cadTypes:FileFilter = new FileFilter("AutoCAD(*.dwg;*.dxb)","*.dwg;*.dxb");
				
				var allTypes:Array = new Array(imageTypes,textTypes,codeTypes,arcTypes,cadTypes);
				file.browse(allTypes);
				
			}
			private function onSelect(e: Event): void{
				Alert.show("上传 " + file.name + " (共 "+Math.round(file.size)+" 字节)?","确认上传",Alert.YES|Alert.NO,null,
					proceedWithUpload);
			}
			
			private function onProgress(e: ProgressEvent): void{
				lbProgress.text = " 已上传 " + e.bytesLoaded
					+ " 字节，共 " + e.bytesTotal + " 字节";
				var proc: uint = e.bytesLoaded / e.bytesTotal * 100;
				bar.setProgress(proc, 100);
				bar.label= "当前进度: " + " " + proc + "%";
			}
			
			private function proceedWithUpload(e: CloseEvent): void{
				if (e.detail == Alert.YES){
					var request: URLRequest = new URLRequest();
				 /* 	Alert.show(this.filelocation);  */
					request.url = "UploadFlie?filepath="+this.filelocation.substring(1)+"/";
					try {
					
						file.upload(request);
					

					} catch (error:Error) {
						trace("上传失败");
					}
					
				}
			}
			
			private function close():void
			{
				MyPopupManager.removePopUp(this);
			}
		]]>
	</mx:Script>
	<mx:VBox width="100%" horizontalAlign="center" fontSize="15">
		<mx:Label id="lbProgress"/>
		<mx:ProgressBar id="bar" labelPlacement="bottom" themeColor="#F20D7A"
						minimum="0" visible="true" maximum="100" label="当前进度: 0%"
						direction="right" mode="manual" width="354"/>
	</mx:VBox>
	<mx:HBox  id="buttomhbox"
			  width="100%" height="40"  verticalAlign="middle" horizontalAlign="center" 
			  backgroundImage="@Embed(source='assets/images/popwindowbtnbg.png')" backgroundSize="100%" x="0" y="76">
		<mx:Button label="选择文件" click="upload();" styleName="loginprimary" width="95"/>
		<mx:Button label="取消" click="close()" styleName="loginsecondary" width="82"/>
	</mx:HBox>
</mx:TitleWindow>